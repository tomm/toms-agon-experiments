/*
    See a thorouhly commented linker file here:
    https://github.com/wntrblm/Castor_and_Pollux/blob/main/firmware/scripts/samd21g18a.ld
*/

RAM_START = 0x0;
RAM_SIZE  = 0x020000;     /* User RAM size */
STACK_SIZE = 0x1000; /* 4KiB stack */

ENTRY(__start)

MEMORY {
    USERRAM (rw) : ORIGIN = RAM_START, LENGTH = RAM_SIZE  /* Define RAM region */
}

SECTIONS
{
    . = RAM_START;

    .init : { *(.init .init.args .init.bss .fini) } > USERRAM

    .text : { *(.text) } > USERRAM

    .init_array : { *(.init_array) } > USERRAM

    .ctors : { *(.ctors) } > USERRAM

    .dtors : { *(.dtors) } > USERRAM

    .fini_array : { *(.fini_array) } > USERRAM

    .rodata : { *(.rodata) } > USERRAM

    .data : { *(.data) } > USERRAM

    .bss :
    {
        bss_start = .;
        *(.bss .bss*)
        bss_end = .;
    }
    __stack = . + STACK_SIZE;
}

//__stack              = bss_end + STACK_SIZE;
___low_bss           = bss_start;
___len_bss           = SIZEOF(.bss);
___heapbot           = bss_end;
___heaptop           = RAM_START + RAM_SIZE;
___run_clearbss      = ___len_bss > 0;

/* symbols for initializing the init_array and ctors sections */
___init_array_count     = SIZEOF(.init_array) / 3;
___ctors_count          = SIZEOF(.ctors) / 3;
___dtors_count          = SIZEOF(.dtors) / 3;
___fini_array_count     = SIZEOF(.fini_array) / 3;
___init_count           = ___init_array_count + ___ctors_count;
___fini_count           = ___dtors_count + ___fini_array_count;
___run_init             = ___init_count > 0;
___run_fini             = ___fini_count > 0;
___init_count_a         = (((___init_count -1) >> 16) & 0xFF) + 1;
___init_count_b         = (((___init_count -1)      ) & 0xFF) + 1;
___init_count_c         = (((___init_count -1) >>  8) & 0xFF) + 1;
___fini_count_a         = (((___fini_count -1) >> 16) & 0xFF) + 1;
___fini_count_b         = (((___fini_count -1)      ) & 0xFF) + 1;
___fini_count_c         = (((___fini_count -1) >>  8) & 0xFF) + 1;
___init_array_functions = SIZEOF(.init_array) + ADDR(.init_array);
___init_ctors_functions = SIZEOF(.ctors) + ADDR(.ctors);
___fini_dtors_functions = SIZEOF(.dtors) + ADDR(.dtors);
___fini_array_functions = SIZEOF(.fini_array) + ADDR(.fini_array);
